
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеПоРегистрам(Отказ, Режим);
	
КонецПроцедуры

//По обычаю выношу процедуры проведения в отдельное место и разбиваю на регистры.
Процедура ПроведениеПоРегистрам(Отказ, Режим)
	
	ИсторияПередачиКартриджей(Отказ, Режим);	
	ОстаткиКартриджей(Отказ, Режим);
	
КонецПроцедуры

Процедура ИсторияПередачиКартриджей(Отказ, Режим)
	
	Движения.ИсторияПередчаиКартриджей.Записывать = Истина;
	
	Для Каждого ТекСтрокаПередача Из Передача Цикл
		
		Движение = Движения.ИсторияПередчаиКартриджей.Добавить();
		Движение.Период = Дата;
		Движение.ПодразделениеИсточник = ОснПодразделение;
		Движение.ПодразделениеПриемник = ПользовПодразделение;
		Движение.Картридж = ТекСтрокаПередача.Картридж;
		Движение.Количество = ТекСтрокаПередача.Количество;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОстаткиКартриджей(Отказ, Режим)
	
	Движения.ОстаткиКартриджей.Записывать = Истина;
	Движения.ОстаткиКартриджей.Очистить();
	
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		
		Движения.ОстаткиКартриджей.БлокироватьДляИзменения = Истина;
		Движения.ОстаткиКартриджей.Записать();
		
	КонецЕсли;      
    	
	Для Каждого ТекСтрокаПередача Из Передача Цикл
		
		Движение = Движения.ОстаткиКартриджей.Добавить();
		Движение.ВидДвижения   = ВидДвиженияНакопления.Расход;
		Движение.Период        = Дата;
		Движение.Картридж      = ТекСтрокаПередача.Картридж;
		Движение.Подразделения = ОснПодразделение;
		Движение.Количество    = ТекСтрокаПередача.Количество;
		
		Движение = Движения.ОстаткиКартриджей.Добавить();
		Движение.ВидДвижения   = ВидДвиженияНакопления.Приход;
		Движение.Период        = Дата;
		Движение.Картридж      = ТекСтрокаПередача.Картридж;
		Движение.Подразделения = ПользовПодразделение;
		Движение.Количество    = ТекСтрокаПередача.Количество;
		
	КонецЦикла;
	
	Движения.Записать();
		
	//КОНТРОЛЬ ОСТАТКОВ	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОстаткиКартриджейОстатки.Картридж,
	               |	ОстаткиКартриджейОстатки.Подразделения,
	               |	ОстаткиКартриджейОстатки.КоличествоОстаток
	               |ИЗ
	               |	РегистрНакопления.ОстаткиКартриджей.Остатки(
	               |			&МоментВремени,
	               |			(Картридж, Подразделения) В
	               |				(ВЫБРАТЬ
	               |					ПриемПередачаКартриджейПередача.Картридж,
	               |					ПриемПередачаКартриджейПередача.Ссылка.ОснПодразделение КАК Подразделение
	               |				ИЗ
	               |					Документ.ПриемПередачаКартриджей.Передача КАК ПриемПередачаКартриджейПередача
	               |				ГДЕ
	               |					ПриемПередачаКартриджейПередача.Ссылка = &Ссылка)) КАК ОстаткиКартриджейОстатки
	               |ГДЕ
	               |	ОстаткиКартриджейОстатки.КоличествоОстаток < 0
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ОстаткиКартриджейОстатки.Картридж
	               |АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Включая));			   
	Запрос.УстановитьПараметр("Ссылка", Ссылка);			   
	
	Результат = Запрос.Выполнить();
	
	//Проверяем остатки, если все списалось то модифицируем документ основание, вставляя в строку признак исполнения спроса.
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
						
			Сообщить("Для картриджа: " 
			+ Выборка.Картридж  
			+ " не хвататет остатка " 
			+ (-Выборка.КоличествоОстаток) 
			+ " шт.",СтатусСообщения.Важное);					
			
			Отказ = Истина;
			
		КонецЦикла;	
		
	Иначе
		
		Если ДокументОснование <> Документы.СпросНаКартриджи.ПустаяСсылка() Тогда			
			ИзменитьДокументСпросаКартриджей(Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СпросНаКартриджи") Тогда
		// Заполнение шапки
		ПользовПодразделение = ДанныеЗаполнения.Подазделение;
		ДокументОснование = ДанныеЗаполнения.Ссылка;
		ОснПодразделение = Склад(); //Справочники.Подразделения.НайтиПоКоду("000000008");
		
		Для Каждого ТекСтрокаКартриджи Из ДанныеЗаполнения.Картриджи Цикл
			
			Если ТекСтрокаКартриджи.Количество - ТекСтрокаКартриджи.Исполнено <> 0 Тогда
				НоваяСтрока = Передача.Добавить();
				НоваяСтрока.Количество = ТекСтрокаКартриджи.Количество - ТекСтрокаКартриджи.Исполнено;
				НоваяСтрока.Картридж = ТекСтрокаКартриджи.МодельКартриджа;				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

//Возвращает склад
Функция Склад()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Подразделения.Ссылка
		|ИЗ
		|	Справочник.Подразделения КАК Подразделения
		|ГДЕ
		|	Подразделения.Склад = ИСТИНА";
	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	
	Возврат ВыборкаДетальныеЗаписи[0].Ссылка; 
	
КонецФункции

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Если ДокументОснование <> Документы.СпросНаКартриджи.ПустаяСсылка() Тогда
		ИзменитьДокументСпросаКартриджей(Ложь);
	КонецЕсли;

КонецПроцедуры

Процедура ИзменитьДокументСпросаКартриджей(Проведение)
	
	ТаблицаСпроса = ДокументОснование.Картриджи.Выгрузить();
	
	Для Каждого Движ Из Передача Цикл
		Для Каждого Строка Из ТаблицаСпроса Цикл
			
			Если Движ.Картридж = Строка.МодельКартриджа Тогда                                                                     
				
				Строка.Исполнено = ?(Проведение, ВернутьКоличествоКартриджей(Строка.МодельКартриджа), Строка.Исполнено - Движ.Количество);
				
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	//Получение объекта
	ДокументОснованиеОбъект = ДокументОснование.ПолучитьОбъект();
	
	//Изменения
	ДокументОснованиеОбъект.Картриджи.Загрузить(ТаблицаСпроса);
	
	//Запись
	ДокументОснованиеОбъект.Записать(РежимЗаписиДокумента.Проведение);	
	
КонецПроцедуры

Функция ВернутьКоличествоКартриджей(Картридж)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПриемПередачаКартриджейПередача.Картридж КАК Картридж,
		|	ПриемПередачаКартриджейПередача.Количество КАК Количество
		|ИЗ
		|	Документ.ПриемПередачаКартриджей.Передача КАК ПриемПередачаКартриджейПередача
		|ГДЕ
		|	ПриемПередачаКартриджейПередача.Ссылка.ДокументОснование = &Ссылка
		|	И ПриемПередачаКартриджейПередача.Картридж = &Картридж
		|ИТОГИ
		|	СУММА(Количество)
		|ПО
		|	Картридж";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОснование);
	Запрос.УстановитьПараметр("Картридж", Картридж);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выгрузить();
	
	Попытка
		Возврат ВыборкаДетальныеЗаписи[0].Количество;
	Исключение
		Возврат 0;
	КонецПопытки;
	
КонецФункции

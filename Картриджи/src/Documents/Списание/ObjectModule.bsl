
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПроведениеПоРегистрам(Отказ, Режим);
		
КонецПроцедуры

Процедура ПроведениеПоРегистрам(Отказ, Режим)
	
	РегистрОстаткиКартриджей(Отказ, Режим);
		
КонецПроцедуры

//Процедуры непосредственной работы с регистрами:
Процедура РегистрОстаткиКартриджей(Отказ, Режим)
	
	//регистр ОстаткиКартриджей Расход
	Движения.ОстаткиКартриджей.Записывать = Истина;
	Движения.ОстаткиКартриджей.Очистить();
	
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		
		Движения.ОстаткиКартриджей.БлокироватьДляИзменения = Истина;
		Движения.ОстаткиКартриджей.Записать();
		
	КонецЕсли;
	
	Для Каждого ТекСтрокаКартриджи Из Картриджи Цикл  		
		
		Движение = Движения.ОстаткиКартриджей.Добавить();
		Движение.ВидДвижения   = ВидДвиженияНакопления.Расход;
		Движение.Период        = Дата;
		Движение.Картридж      = ТекСтрокаКартриджи.Картриджи;
		Движение.Количество    = ТекСтрокаКартриджи.Количество;
		Движение.Подразделения = Подразделение;		
		
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОстаткиКартриджейОстатки.Картридж КАК Картридж,
	               |	ОстаткиКартриджейОстатки.Подразделения КАК Подразделение,
	               |	ОстаткиКартриджейОстатки.КоличествоОстаток
	               |ИЗ
	               |	РегистрНакопления.ОстаткиКартриджей.Остатки(
	               |			&МоментВремени,
	               |			(Картридж, Подразделения) В
	               |				(ВЫБРАТЬ
	               |					СписаниеКартриджи.Картриджи,
	               |					СписаниеКартриджи.Ссылка.Подразделение
	               |				ИЗ
	               |					Документ.Списание.Картриджи КАК СписаниеКартриджи
	               |				ГДЕ
	               |					СписаниеКартриджи.Ссылка = &Ссылка)) КАК ОстаткиКартриджейОстатки
	               |ГДЕ
	               |	ОстаткиКартриджейОстатки.КоличествоОстаток < 0
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Картридж
	               |АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(МоментВремени(), ВидГраницы.Включая));			   
	Запрос.УстановитьПараметр("Ссылка"       , Ссылка);			   
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Сообщить("В подразделении: " 
			+ Выборка.Подразделение 
			+ ", для картриджа: " 
			+ Выборка.Картридж 
			+ ",не хвататет количества: " 
			+ Строка(-Выборка.КоличествоОстаток) 
			+ " шт..",СтатусСообщения.Важное);		
			
			Отказ = Истина;
			
		КонецЦикла;	
	
	КонецЕсли;
	
КонецПроцедуры

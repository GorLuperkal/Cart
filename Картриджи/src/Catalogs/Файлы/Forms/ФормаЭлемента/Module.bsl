
&НаСервере
Процедура УстановитьВидимость()

	Если Объект.ВидДанных = ПредопределенноеЗначение("Перечисление.ВидыДополнительнойИнформацииОбъектов.Изображение") Тогда
		Элементы.Картинка.Видимость = Истина;
	Иначе	
		Элементы.Картинка.Видимость = Ложь;	
	КонецЕсли;                               	

КонецПроцедуры 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Если Параметры.Свойство("ЗначенияЗаполнения") Тогда
			
			ЗаполнитьЗначенияСвойств(Объект,Параметры.ЗначенияЗаполнения);
			
		КонецЕсли;	
		
	КонецЕсли;    	 
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ЗагрузитьФайл(0);
		
	Иначе	
		
		Адрес = ПолучитьНавигационнуюСсылку(Объект.Ссылка,"Хранилище");
		Элементы.Картинка.РазмерКартинки = РазмерКартинки.АвтоРазмер; 
		
	КонецЕсли;	                                                      	
		
КонецПроцедуры
    
&НаКлиенте
Процедура ЗагрузитьФайл(Команда)
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);	
		
	Если ПредопределенноеЗначение("Перечисление.ВидыДополнительнойИнформацииОбъектов.Изображение") = Объект.ВидДанных Тогда
		ДиалогОткрытияФайла.Заголовок                   = "Выберите изображение...";
		ДиалогОткрытияФайла.Фильтр                      = РаботаСФайламиКлиент.ПолучитьФильтрИзображений();	
	Иначе	
		ДиалогОткрытияФайла.Заголовок                   = "Выберите файл...";
		ДиалогОткрытияФайла.Фильтр                      = РаботаСФайламиКлиент.ПолучитьФильтрФайлов();	
	КонецЕсли; 
	
	ДиалогОткрытияФайла.ПредварительныйПросмотр     = Истина;
	ДиалогОткрытияФайла.ПроверятьСуществованиеФайла = Истина;
	ДиалогОткрытияФайла.МножественныйВыбор          = Ложь;
	
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		
		Файл = Новый Файл(ДиалогОткрытияФайла.ПолноеИмяФайла);
		Объект.Наименование   = Файл.Имя;
		Объект.ИмяФайла       = Файл.Имя;
		Объект.Расширение     = Файл.Расширение;
		Объект.ИндексКартинки = РаботаСФайламиКлиент.ПолучитьИндексПиктограммыФайла(Файл.Расширение);
		
		Адрес = "";		
		ПоместитьФайл(Адрес, ДиалогОткрытияФайла.ПолноеИмяФайла, , Ложь , УникальныйИдентификатор);
		
		Если ПредопределенноеЗначение("Перечисление.ВидыДополнительнойИнформацииОбъектов.Изображение") = Объект.ВидДанных Тогда
			Элементы.Картинка.РазмерКартинки = РазмерКартинки.АвтоРазмер;
		КонецЕсли;	
		
	КонецЕсли;   
		
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайл(Команда)
	
	ПолучитьФайл(Адрес, Объект.ИмяФайла) ;
	 
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАдресВременногоХранилища(Адрес) Тогда
		
		ТекущийОбъект.Хранилище = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Адрес));
	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	РаботаСФайламиКлиент.ОткрытьФайлы(Объект.ИмяФайла, Объект.Ссылка, Ложь);
	
КонецПроцедуры
     
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("УстановитьОсновноеИзображениеЕслиЕгоНет",Объект.ОбъектХранилища,Объект.Ссылка);
	
КонецПроцедуры
 
